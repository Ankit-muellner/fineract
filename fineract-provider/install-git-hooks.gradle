/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements. See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership. The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
static def isLinuxOrMacOs() {
    def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
    return osName.contains('linux') || osName.contains('mac os') || osName.contains('macos')
}

def gitDir = new File(rootProject.projectDir, "../")

task copyGitHooks(type: Copy) {
    description 'Copies the git hooks from props/git-hooks to the .git folder.'
    from("${gitDir}/props/") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${gitDir}/.git/hooks"
    onlyIf { isLinuxOrMacOs() }
}


task installGitHooks(type: Exec) {
    description 'Installs the pre-commit git hooks from team-props/git-hooks.'
    group 'git hooks'
    workingDir gitDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
    onlyIf { isLinuxOrMacOs() }
    doLast {
        logger.info('Git hook installed successfully.')
    }
}